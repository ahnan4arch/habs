# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

pkgname=ghc
pkgver=8.0.1
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
depends=('perl' 'gmp>=5.0' 'gcc')
makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl' 'happy' 'alex')
checkdepends=('python2')
install='ghc.install'
options=('strip' 'staticlibs')

provides=('haskell-array=0.5.1.1'
          'haskell-base=4.9.0.0'
          'haskell-binary=0.8.3.0'
          'haskell-bytestring=0.10.8.1'
          'haskell-cabal=1.24.0.0'
          'haskell-containers=0.5.7.1'
          'haskell-deepseq=1.4.2.0'
          'haskell-directory=1.2.6.2'
          'haskell-filepath=1.4.1.0'
          'haskell-ghc-boot-th=8.0.1'
          'haskell-ghc-boot=8.0.1'
          'haskell-ghc-prim=0.5.0.0'
          'haskell-ghc=8.0.1'
          'haskell-ghci=8.0.1'
          'haskell-haskeline=0.7.2.3'
          'haskell-hoopl=3.10.2.1'
          'haskell-hpc=0.6.0.3'
          'haskell-integer-gmp=1.0.0.1'
          'haskell-pretty=1.1.3.3'
          'haskell-process=1.4.2.0'
          'haskell-rts=1.0'
          'haskell-template-haskell=2.11.0.0'
          'haskell-terminfo=0.4.0.2'
          'haskell-time=1.6.0.1'
          'haskell-transformers=0.5.2.0'
          'haskell-unix=2.7.2.0'
          'haskell-xhtml=3000.2.1')
replaces=('haskell-array<0.5.1.1'
          'haskell-base<4.9.0.0'
          'haskell-binary<0.8.3.0'
          'haskell-bytestring<0.10.8.1'
          'haskell-cabal<1.24.0.0'
          'haskell-containers<0.5.7.1'
          'haskell-deepseq<1.4.2.0'
          'haskell-directory<1.2.6.2'
          'haskell-filepath<1.4.1.0'
          'haskell-ghc-boot-th<8.0.1'
          'haskell-ghc-boot<8.0.1'
          'haskell-ghc-prim<0.5.0.0'
          'haskell-ghc<8.0.1'
          'haskell-ghci<8.0.1'
          'haskell-haskeline<0.7.2.3'
          'haskell-hoopl<3.10.2.1'
          'haskell-hpc<0.6.0.3'
          'haskell-integer-gmp<1.0.0.1'
          'haskell-pretty<1.1.3.3'
          'haskell-process<1.4.2.0'
          'haskell-rts<1.0'
          'haskell-template-haskell<2.11.0.0'
          'haskell-terminfo<0.4.0.2'
          'haskell-time<1.6.0.1'
          'haskell-transformers<0.5.2.0'
          'haskell-unix<2.7.2.0'
          'haskell-xhtml<3000.2.1')

source=("https://downloads.haskell.org/~ghc/${pkgver}/ghc-${pkgver}-src.tar.xz"
        "https://downloads.haskell.org/~ghc/${pkgver}/ghc-${pkgver}-testsuite.tar.xz"
        'build.mk' 'ghc-rebuild-doc-index.hook' 'arch-gen-contents-index')
sha256sums=('90fb20cd8712e3c0fbeb2eac8dab6894404c21569746655b9b12ca9684c7d1d2'
            'bc57163656ece462ef61072559d491b72c5cdd694f3c39b80ac0f6b9a3dc8151'
            'b528fca737d38fae87eadfbef94b3a29c9f62e9936dc8c28af03fd36d281d267'
            'd0e8019ea270ae176e4ddb10327a58c0c8d145a1f0913df2f7da84300646fa61'
            'cb1a9c66c241fd702f595c08d79b1afc37797434ad4f9544c8fe0503e3a27a38')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    cp "${srcdir}/build.mk" mk/build.mk
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr
    make -j 10
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # python2 rename
    sed -e 's_#!/usr/bin/env python_&2_' -i testsuite/timeout/calibrate testsuite/timeout/timeout.py

    #make test
    make THREADS=9 test

    # zero unexpected failures on all tier 1 platforms - http://hackage.haskell.org/trac/ghc/ticket/5757
    # enable this when upstream has 0 test failures from a simple 'make test'
    #make fulltest
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR=${pkgdir} install

    # clean up the doc index, it's re-generated in ghc.install
    ( cd ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/html/libraries ;
    rm doc-index*.html frames.html index-frames.html index.html \
        ocean.css haddock-util.js \
        hslogo-16.png  minus.gif plus.gif synopsis.png
    )
    # install our own contents generator
    install -m755 ${srcdir}/arch-gen-contents-index \
        ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/html/libraries

    install -d ${pkgdir}/usr/share/licenses/ghc
    install -m644 LICENSE \
        ${pkgdir}/usr/share/licenses/ghc

    install -D -m644 ${srcdir}/ghc-rebuild-doc-index.hook \
            ${pkgdir}/usr/share/libalpm/hooks/ghc-rebuild-doc-index.hook
}
